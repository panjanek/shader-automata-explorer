#version 430 core

#pragma optimize(on)

layout(local_size_x = {LocalSizeX}) in;

struct SpeciesConfig
{
    float velocity;
    float turnSpeed;
    float sensorAngle;
    float sensorDistance;
    int sensorSize;
};

struct ShaderConfig {
    int agentsCount;
    int width;
    int height;
    int time;
    SpeciesConfig species_r;
    SpeciesConfig species_g;
    SpeciesConfig species_b;
};

struct Agent
{
    vec2 position;
    float angle;
    uint species;
};

layout(std430, binding = 0) buffer ConfigBuffer {
    ShaderConfig config;
};

layout(std430, binding = 1) buffer OutputBuffer {
    Agent agents[];
};

layout(rgba32f, binding = 3) uniform image2D uStateImage;

void torus(inout vec2 pos)
{
    if (pos.x > config.width)
        pos.x -= config.width;
    else if (pos.x < 0)
        pos.x += config.width;

    if (pos.y > config.height)
        pos.y -= config.height;
    else if (pos.y < 0)
        pos.y += config.height;
}

void itorus(inout ivec2 pos)
{
    if (pos.x > config.width)
        pos.x -= config.width;
    else if (pos.x < 0)
        pos.x += config.width;

    if (pos.y > config.height)
        pos.y -= config.height;
    else if (pos.y < 0)
        pos.y += config.height;
}

uint hash(uint state)
{
    state ^= 2747636419u;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    return state;
}

float scaleToRange01(uint state)
{
    return state / 4294967295.0;
}

vec4 sense(Agent agent, float sensorAngle, float sensorDist, int sensorSize)
{
    float sensingAngle = agent.angle + sensorAngle;
    vec2 sensingDir = vec2(cos(sensingAngle),sin(sensingAngle));
    vec2 sensingCenter = agent.position + sensingDir * sensorDist;
    ivec2 center = ivec2(int(sensingCenter.x),int(sensingCenter.y));
    vec4 sum = vec4(0,0,0,0);
    for(int offsetX=-sensorSize; offsetX <= sensorSize; offsetX++)
        for(int offsetY=-sensorSize; offsetY <= sensorSize; offsetY++)
        {
            ivec2 sensePixel = center + ivec2(offsetX, offsetY);
            itorus(sensePixel);
            sum += imageLoad(uStateImage, sensePixel);
        }

    return sum;
}

//https://github.com/SebLague/Slime-Simulation/blob/main/Assets/Scripts/Slime/SlimeSim.compute

void update_one(uint idx)
{
    Agent agent = agents[idx];
    SpeciesConfig spec = config.species_r;
    if (agent.species == 1)
        spec = config.species_g;
    else if (agent.species == 2)
        spec = config.species_b;

    vec4 senseLeftMulti = sense(agent, -spec.sensorAngle, spec.sensorDistance, spec.sensorSize);
    vec4 senseForwardMulti = sense(agent, 0, spec.sensorDistance, spec.sensorSize);
    vec4 senseRightMulti = sense(agent, spec.sensorAngle, spec.sensorDistance, spec.sensorSize);

    float senseLeft = senseLeftMulti.r;
    float senseForward = senseForwardMulti.r;
    float senseRight = senseRightMulti.r;
    if (agent.species == 1)
    {
        senseLeft = senseLeftMulti.g;
        senseForward = senseForwardMulti.g;
        senseRight = senseRightMulti.g;
    }
    else if (agent.species == 2)
    {
        senseLeft = senseLeftMulti.b;
        senseForward = senseForwardMulti.b;
        senseRight = senseRightMulti.b;
    }

    float senseLeftDifferent = max(senseLeftMulti.g, senseLeftMulti.b);
    float senseForwardDifferent = max(senseForwardMulti.g, senseForwardMulti.b);
    float senseRightDifferent = max(senseRightMulti.g, senseRightMulti.b);
    if (agent.species == 1)
    {
        senseLeftDifferent = max(senseLeftMulti.r, senseLeftMulti.b);
        senseForwardDifferent = max(senseForwardMulti.r, senseForwardMulti.b);
        senseRightDifferent = max(senseRightMulti.r, senseRightMulti.b);
    }
    else if (agent.species == 2)
    {
        senseLeftDifferent = max(senseLeftMulti.r, senseLeftMulti.g);
        senseForwardDifferent = max(senseForwardMulti.r, senseForwardMulti.g);
        senseRightDifferent = max(senseRightMulti.r, senseRightMulti.g);
    }

    uint random =  hash(uint(agent.position.y * config.width + agent.position.x) + hash(gl_GlobalInvocationID.x + config.time * 100000));
    float randomSteerStrength = scaleToRange01(random);

    float angleDelta = 0;

    // attract to the same color
    if (senseForward > senseLeft && senseForward > senseRight){
        


            if (senseRightDifferent > senseLeftDifferent) {
                angleDelta += -spec.turnSpeed * (0.75+0.5*randomSteerStrength);
            } 
            else if (senseLeftDifferent > senseRightDifferent){
                angleDelta += spec.turnSpeed * (0.75+0.5*randomSteerStrength);
            } 


    }
    else if (senseForward < senseLeft && senseForward < senseRight) {
        angleDelta += (randomSteerStrength - 0.5) * 2 * spec.turnSpeed;
    } else if (senseRight > senseLeft) {
        angleDelta += spec.turnSpeed * (0.75+0.5*randomSteerStrength);
    } 
    else if (senseLeft > senseRight){
        angleDelta += -spec.turnSpeed * (0.75+0.5*randomSteerStrength);
    } 








    agent.angle += angleDelta;   
    agent.position += vec2(spec.velocity*cos(agent.angle),spec.velocity*sin(agent.angle));
    torus(agent.position);
    agents[idx] = agent;
}

void write_color(uint idx)
{
    Agent agent = agents[idx];
    ivec2 pixelPos = ivec2(uint(agent.position.x), uint(agent.position.y));
    vec4 current = imageLoad(uStateImage, pixelPos);
    if (agent.species == 0)
        current.r = 1.0;
    else if (agent.species == 1)
        current.g = 1.0;
    else
        current.b = 1.0;
    imageStore(uStateImage, pixelPos, current); 
}

void main()
{
    uint idx = gl_GlobalInvocationID.x;

    if (idx >=0 && idx < config.agentsCount) {

        update_one(idx);
        write_color(idx);
    }
}